apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'com.ifnoelse'
version '1.0'

sourceCompatibility = 1.8

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = 'ifnoelse'
                description = 'util collections'
                url = 'https://github.com/ifnoelse/ifnoelse'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ifnoelse'
                        name = 'ifnoelse'
                        email = 'ifnoelse@qq.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/ifnoelse/ifnoelse.git'
                    developerConnection = 'scm:git:https://github.com/ifnoelse/ifnoelse.git'
                    url = 'https://github.com/ifnoelse/ifnoelse'
                }
            }
        }
    }
    repositories {
        maven {
            url = './maven'
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
}
